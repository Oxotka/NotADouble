
&НаКлиенте
Процедура ДублиКонтрагентовПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("ПропуститьПроверку") 
		И ПараметрыЗаписи.ПропуститьПроверку Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоФизЛицо = Объект.ЮридическоеФизическоеЛицо = ПредопределенноеЗначение("Перечисление.ЮридическоеФизическоеЛицо.ФизическоеЛицо");
	
	Если (Объект.ИННВведенКорректно и Объект.КППВведенКорректно) 
		или (Объект.ИННВведенКорректно и ЭтоФизЛицо) Тогда 
		
		КоличествоЭлементовДублей = ВыполнитьПоискДублейСервер(Объект.ИНН, Объект.КПП, Объект.Ссылка);
		
		ЕстьДубли = КоличествоЭлементовДублей > 0; 
		
		Если ЕстьДубли Тогда
			
			Отказ = Истина;
			
			ТекстПредупреждения = НСтр("ru = 'Уже есть элементы с таким %1. 
				|Чтобы записать нажмите ""Еще"" - ""Записать дубль""'");
			Если ЭтоФизЛицо Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, НСтр("ru = 'ИНН'"));
			Иначе
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, НСтр("ru = 'ИНН и КПП'"));
			КонецЕсли;
			
			ПоказатьПредупреждение(, ТекстПредупреждения);
			
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДублиКонтрагентовЗаписатьДубль(Команда)
	
	ПараметрыЗаписи = Новый Структура;
	ПараметрыЗаписи.Вставить("ПропуститьПроверку", Истина);
	Записать(ПараметрыЗаписи);
	
	КоличествоЭлементовДублей = ВыполнитьПоискДублейСервер(Объект.ИНН, Объект.КПП, Объект.Ссылка);

	РезультатПроверкиНаДубли = ОписаниеРезультатаПроверкиДублей(ЭтотОбъект, КоличествоЭлементовДублей);
	
	ОбщегоНазначенияБПКлиентСервер.ОтобразитьРезультатПроверкиКонтрагентовВФорме(ЭтотОбъект, "");
	
	// Устанавливаем текст заголовка
	Если ПроверкаКонтрагентовПоИННКППВозможна Тогда
		// СтандартныеПодсистемы.РаботаСКонтрагентами
		ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВСправочнике(ЭтотОбъект);
		// Конец СтандартныеПодсистемы.РаботаСКонтрагентами
	Конецесли;

КонецПроцедуры
